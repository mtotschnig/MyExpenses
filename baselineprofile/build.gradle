plugins {
    id 'com.android.test'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.baselineprofile'
}

android {
    namespace 'org.totschnig.myexpenses.baselineprofile'
    compileSdk 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        minSdk 28
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    targetProjectPath = ":myExpenses"

    flavorDimensions += ["textRecognition"]
    productFlavors {
        intern { dimension = "textRecognition" }
        extern { dimension = "textRecognition" }
    }

}

// This is the configuration block for the Baseline Profile plugin.
// You can specify to run the generators on a managed devices or connected devices.
baselineProfile {
    useConnectedDevices = true
}

dependencies {
    implementation 'androidx.test.ext:junit:1.1.5'
    implementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    implementation 'androidx.benchmark:benchmark-macro-junit4:1.2.4'
}

androidComponents {
    onVariants(selector().all()) { v ->
        def artifactsLoader = v.artifacts.getBuiltArtifactsLoader()
        v.instrumentationRunnerArguments.put(
                "targetAppId",
                v.testedApks.map { artifactsLoader.load(it)?.applicationId }
        )
    }
}