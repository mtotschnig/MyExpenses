apply plugin: 'com.android.application'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        applicationId "org.totschnig.myexpenses"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
        versionCode 240
        versionName '2.6.5.1'

        buildConfigField "String", "PLATTFORM", "\"Android\""
        buildConfigField "String", "BUILD_DATE", "\"" + getDate() + "\""
        testInstrumentationRunner "org.totschnig.myexpenses.MyTestRunner"
    }

    /*signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }*/
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            //signingConfig signingConfigs.release

        }
    }

    lintOptions {
        disable 'InvalidPackage', 'MissingQuantity', 'MissingTranslation', 'ImpliedQuantity'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    //testBuildType "release"
}

dependencies {
    compile project(':StickyListHeaders:library')
    compile project(':MPAndroidChart:MPChartLib')
    compile project(':MaterialSeekBarPreference:library')
    compile 'com.android.support:appcompat-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:support-v4:' + project.androidSupportLibraryVersion
    compile 'com.android.support:recyclerview-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:cardview-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:design:' + project.androidSupportLibraryVersion
    compile 'com.android.support:gridlayout-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:preference-v14:' + project.androidSupportLibraryVersion
    compile 'com.itextpdf:itextpdf:5.5.8'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-csv:1.1'
    //noinspection GradleDynamicVersion
    compile 'com.commonsware.cwac:wakeful:1.0.+'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:' + project.espressoVersion
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:'  + project.androidSupportLibraryVersion
    androidTestCompile 'com.android.support.test:runner:' + project.androidSupportTestVersion
    androidTestCompile 'com.android.support.test:rules:' + project.androidSupportTestVersion
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:' + project.espressoVersion
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:' + project.espressoVersion) {
        exclude module: 'accessibility-test-framework'
    }

    testCompile 'junit:junit:4.12'
}
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HHmm')
    return formattedDate
}

//https://code.google.com/p/android/issues/detail?id=187730
//can be removed after migration to Android Gradle Plugin 2
configurations.compile.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
    }
}

