apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        applicationId "org.totschnig.myexpenses"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 275
        versionName '2.7.7'
        multiDexEnabled true

        buildConfigField "String", "PLATTFORM", "\"Android\""
        buildConfigField "String", "BUILD_DATE", "\"" + getDate() + "\""
        testInstrumentationRunner "org.totschnig.myexpenses.MyTestRunner"
        testProguardFile "proguard-test.txt"

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    /*signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }*/
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            //signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name", debug: "false"]
            resValue "string", "content_authority", "org.totschnig.myexpenses"
            resValue "string", "sync_account_type", "org.totschnig.myexpenses.sync"
        }
        beta {
            initWith release
            applicationIdSuffix ".beta"
            resValue "string", "content_authority", "org.totschnig.myexpenses.beta"
            resValue "string", "sync_account_type", "org.totschnig.myexpenses.beta.sync"
        }
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "My Expenses Debug", debug: "true"]
            resValue "string", "content_authority", "org.totschnig.myexpenses.debug"
            resValue "string", "sync_account_type", "org.totschnig.myexpenses.debug.sync"
        }
        forTest {
            initWith release
        }
    }

    lintOptions {
        disable 'InvalidPackage', 'MissingQuantity', 'MissingTranslation', 'ImpliedQuantity'
    }

    dexOptions {
        javaMaxHeapSize "2048M"
        jumboMode = true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    testBuildType "forTest"
}

dependencies {
    apt 'com.google.dagger:dagger-compiler:' + project.daggerVersion
    compile 'com.google.dagger:dagger:' + project.daggerVersion
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'frankiesardo:icepick:3.2.0'
    apt 'frankiesardo:icepick-processor:3.2.0'
    compile 'com.google.code.gson:gson:2.8.0'
    provided 'com.google.auto.value:auto-value:1.4'
    apt 'com.google.auto.value:auto-value:1.4'
    apt 'com.ryanharter.auto.value:auto-value-gson:0.4.6'
    provided 'com.ryanharter.auto.value:auto-value-gson:0.4.6'
    apt 'com.gabrielittner.auto.value:auto-value-cursor:1.0.1'
    compile 'com.gabrielittner.auto.value:auto-value-cursor-annotations:1.0.0'
    compile 'com.github.mtotschnig:StickyListHeaders:master-SNAPSHOT'
    compile 'com.pavelsikun:material-seekbar-preference:2.3.0'
    compile 'com.gitlab.bitfireAT:dav4android:master-SNAPSHOT'
    compile 'com.android.support:appcompat-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:support-v4:' + project.androidSupportLibraryVersion
    compile 'com.android.support:recyclerview-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:cardview-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:design:' + project.androidSupportLibraryVersion
    compile 'com.android.support:gridlayout-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:preference-v14:' + "23.1.1"
    compile 'com.itextpdf:itextg:5.5.10'
    compile 'ch.acra:acra:4.9.2'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-csv:1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.PhilJay:MPAndroidChart:bd75e6c'
    compile 'com.github.mtotschnig:Caldroid:master-SNAPSHOT'
    //noinspection GradleDynamicVersion
    compile 'com.commonsware.cwac:wakeful:1.0.+'
    compile 'com.annimon:stream:1.1.6'

    // WebDAV
    compile 'com.squareup.okhttp3:okhttp:3.4.2'

    //noinspection GradleCompatible
    compile 'com.android.support:multidex:1.0.1'

    compile 'org.apache.commons:commons-collections4:4.1'

    androidTestCompile 'com.android.support.test.espresso:espresso-core:' + project.espressoVersion
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:'  + project.androidSupportLibraryVersion
    androidTestCompile 'com.android.support.test:runner:' + project.androidSupportTestVersion
    androidTestCompile 'com.android.support.test:rules:' + project.androidSupportTestVersion
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:' + project.espressoVersion
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:' + project.espressoVersion) {
        exclude module: 'accessibility-test-framework'
    }
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'

    testCompile 'junit:junit:4.12'
}
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HHmm')
    return formattedDate
}

//https://code.google.com/p/android/issues/detail?id=187730
//can be removed after migration to Android Gradle Plugin 2
configurations.compile.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
    }
}

