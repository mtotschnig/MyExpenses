import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.android.dynamic.feature apply false
    alias libs.plugins.jetbrains.kotlin.android apply false
    alias libs.plugins.ksp apply false
    alias libs.plugins.compose.compiler apply false
    alias libs.plugins.kotlin.serialization apply false
    //drive
    //id 'com.google.gms.google-services' version '4.4.0' apply false
}

ext {
    resConfig = ['en', 'en-rGB', 'ar', 'be', 'bg', 'bn', 'ca', 'cs', 'da', 'de', 'de-rDE', 'el', 'es', 'eu', 'fr', 'fr-rCA', 'iw', 'hr', 'hu', 'it', 'ja', 'km', 'kn', 'ko', 'ms', 'nl', 'pl', 'pt-rBR', 'pt-rPT', 'ro', 'ru', 'si', 'ta', 'te', 'th', 'tr', 'uk', 'ur', 'vi', 'zh-rCN', 'zh-rHK', 'zh-rTW']
    versionCode = 815
    versionName = '4.0.4.1'
    compileSdkVersion = 36
    minSdkVersion = 21
    targetSdkVersion = 36
}

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("composeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
            if (project.findProperty("composeCompilerMetrics") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
        }
    }
}

allprojects { // Or subprojects {
    configurations {
        all { Configuration configuration ->
            configuration.exclude(group: 'com.google.code.findbugs', module: 'jsr305')
        }
    }
}

