plugins {
    id 'com.android.dynamic-feature'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}
android {
    compileSdk project.compileSdkVersion

    defaultConfig {
        missingDimensionStrategy 'textRecognition', 'extern'
        minSdk project.minSdkVersion
        consumerProguardFiles("consumer-rules.txt")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resourceConfigurations = resConfig
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    packagingOptions {
        resources {
            excludes += ["META-INF/services/reactor.blockhound.integration.BlockHoundIntegration"]
            merges -= "/META-INF/services/**"
        }
    }
    namespace 'org.totschnig.onedrive'
}

dependencies {
    coreLibraryDesugaring libs.android.desugar
    implementation project(":myExpenses")
    implementation project(":jackson")
    implementation libs.android.state
    kapt libs.android.state.processor

    implementation('com.github.mtotschnig:microsoft-authentication-library-for-android:ee8eeef079') {
        exclude group: 'com.microsoft.device.display'
        exclude group: 'org.json'
    }

    implementation libs.microsoft.graph
}

configurations.configureEach {
    exclude group:"com.google.guava", module:"guava"
    //exclude group:"androidx.credentials", module:"credentials-play-services-auth"

    resolutionStrategy {
        dependencySubstitution {
            substitute module("com.azure:azure-core") using module("com.github.mtotschnig.Azure-Core:azure-core:0ccd6e7285")
        }
    }
}